plugins {
  id 'application'
  id 'jacoco'
  id 'com.gradleup.shadow' version '8.3.1'
  id 'se.thinkcode.cucumber-runner' version '0.0.11'
  id 'com.github.spotbugs' version '6.0.24'
}


repositories {
    mavenCentral()
}

dependencies {
    testImplementation libs.junit.jupiter
    testImplementation(platform("io.cucumber:cucumber-bom:7.19.0"))
    testImplementation("io.cucumber:cucumber-java")
    testImplementation("io.cucumber:cucumber-junit-platform-engine")
    testImplementation 'org.hamcrest:hamcrest:3.0'
    testImplementation 'org.assertj:assertj-swing:3.17.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.6.1'
    testImplementation "org.mockito:mockito-inline:+"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'info.picocli:picocli:4.7.6'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


application {
    mainClass = 'scrumroyale.App'
}


jar {
    archiveBaseName = 'app'
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}


shadowJar {
   archiveBaseName.set(rootProject.name)
   archiveClassifier.set('')
   archiveVersion.set('')
}

tasks.named('test') {
    useJUnitPlatform()
}

run {
    standardInput = System.in
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        html {
            enabled true
        }
        xml {
            enabled true
        }
    }
}

javadoc {
    options.with {
        links 'https://docs.oracle.com/javase/17/docs/api/'
    }
}

cucumber {
    glue = 'gradle.cucumber'
    plugin = ['pretty', 'html:app/build/reports/cucumber/report.html']
    main = 'io.cucumber.core.cli.Main'
}

spotbugs {
  excludeFilter.set(
      file("$rootDir/config/spotbugs-exclude.xml")
  )
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

